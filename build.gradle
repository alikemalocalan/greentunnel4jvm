buildscript {

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

group = projectGroup
version = projectVersion

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'application'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    compileKotlin.kotlinOptions.jvmTarget = "1.8"
    compileTestKotlin.kotlinOptions.jvmTarget = "1.8"
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }

    }
}

application {
    mainClassName = projectMainClassName
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compile 'io.netty:netty-handler-proxy:4.1.56.Final'
    compile 'com.squareup.okhttp3:okhttp:4.9.0'
    compile 'com.squareup.okhttp3:okhttp-dnsoverhttps:4.9.0'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
}

jar {
    from 'resources', {
        into 'resources'
    }
    manifest {
        attributes 'Main-Class': projectMainClassName
    }
}
pom {
    project {
        inceptionYear '2008'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
    }
}.writeTo("pom.xml")

task copyResources(type: Copy) {
    from "${projectDir}/src/main/resources"
    into "${buildDir}/classes/kotlin/main/META-INF"
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('')
    archiveVersion.set('')
}
shadowJar.dependsOn copyResources

task sourcesJar(type: Jar, dependsOn: classes) {
    //noinspection GroovyAccessibility //alternatively replace this with archiveClassifier.set('...')
    archiveClassifier = 'sources'
    from sourceSets.main.allSource, 'build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main'
}
publishing {
    publications {
        githubPublish(MavenPublication) {
            artifactId projectName
            version projectVersion

            //from components.java
            artifact(sourcesJar)
            artifact(jar)

            pom {
                name = projectName
                description = 'Green Tunnel Alternative for JVM Languages'
                url = 'https://github.com/alikemalocalan/greentunnel4jvm'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://mit-license.org/'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'alikemalocalan'
                        name = 'Ali Kemal Ã–calan'
                        email = 'alikemalocalan@gmail.com'
                        roles = ['owner', 'developer']
                    }
                }
                scm {
                    url = 'https://github.com/alikemalocalan/greentunnel4jvm'
                    connection = 'scm:git:https://github.com/alikemalocalan/greentunnel4jvm.git'
                    developerConnection = 'scm:git:ssh://git@github.com/alikemalocalan/greentunnel4jvm.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/alikemalocalan/greentunnel4jvm")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}