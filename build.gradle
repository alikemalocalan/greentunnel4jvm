import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.20'
    id 'maven-publish'
    id 'application'
    id 'com.gradleup.shadow' version '9.0.0-beta12'
}

group = projectGroup
version = projectVersion

application {
    mainClass.set(projectMainClassName)
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    api 'io.netty:netty-transport:4.2.0.Final'
    api 'dnsjava:dnsjava:3.6.3'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    compilerOptions {
        apiVersion = KotlinVersion.KOTLIN_2_1
        jvmTarget = JvmTarget.JVM_17
    }
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('')
    archiveVersion.set('')
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/alikemalocalan/greentunnel4jvm")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
