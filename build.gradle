buildscript {

    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
       classpath 'com.github.johnrengelman:shadow:8.1.1'
    }
}

group = projectGroup
version = projectVersion

allprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
//  apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'application'
    apply plugin: "com.github.johnrengelman.shadow"

    sourceCompatibility = 11
    targetCompatibility = 11
    compileKotlin.kotlinOptions.jvmTarget = "11"
    compileTestKotlin.kotlinOptions.jvmTarget = "11"
    compileJava.options.encoding = 'UTF-8'

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }

    }
}

application {
    mainClassName = projectMainClassName
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api 'io.netty:netty-transport:4.1.109.Final'
    api 'com.squareup.okhttp3:okhttp-dnsoverhttps:4.12.0'
    implementation 'ch.qos.logback:logback-classic:1.5.3'
}

/* pom {
    project {
        inceptionYear '2008'
        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
    }
}.writeTo("pom.xml") */


kotlin {
    jvmToolchain(11)
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('')
    archiveVersion.set('')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    //noinspection GroovyAccessibility //alternatively replace this with archiveClassifier.set('...')
    archiveClassifier = 'sources'
    from sourceSets.main.allSource, 'build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main'
}
publishing {
    publications {
        githubPublish(MavenPublication) {
            artifactId projectName
            version projectVersion

            //from components.java
            artifact(sourcesJar)
            artifact(jar)

            pom {
                name = projectName
                description = 'Green Tunnel Alternative for JVM Languages'
                url = 'https://github.com/alikemalocalan/greentunnel4jvm'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://mit-license.org/'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'alikemalocalan'
                        name = 'Ali Kemal Ã–calan'
                        email = 'alikemalocalan@gmail.com'
                        roles = ['owner', 'developer']
                    }
                }
                scm {
                    url = 'https://github.com/alikemalocalan/greentunnel4jvm'
                    connection = 'scm:git:https://github.com/alikemalocalan/greentunnel4jvm.git'
                    developerConnection = 'scm:git:ssh://git@github.com/alikemalocalan/greentunnel4jvm.git'
                }
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/alikemalocalan/greentunnel4jvm")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}