import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.2.10'
    id 'maven-publish'
    id 'application'
    id 'com.gradleup.shadow' version '9.1.0'
}

group = projectGroup
version = projectVersion

application {
    mainClass.set(projectMainClassName)
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    api 'io.netty:netty-transport:4.2.6.Final'
    api 'dnsjava:dnsjava:3.6.3'
    implementation 'ch.qos.logback:logback-classic:1.5.18'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    compilerOptions {
        apiVersion = KotlinVersion.KOTLIN_2_1
        jvmTarget = JvmTarget.JVM_17
    }
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('')
    archiveVersion.set('')
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}
publishing {
    publications {
        githubPublish(MavenPublication) {
            artifactId projectName
            version projectVersion
            //from components.java
            artifact(sourcesJar)
            artifact(jar)
            pom {
                name = projectName
                description = 'Green Tunnel Alternative for JVM Languages'
                url = 'https://github.com/alikemalocalan/greentunnel4jvm'
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://mit-license.org/'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'alikemalocalan'
                        name = 'Ali Kemal Ã–calan'
                        email = 'alikemalocalan@gmail.com'
                        roles = ['owner', 'developer']
                    }
                }
                scm {
                    url = 'https://github.com/alikemalocalan/greentunnel4jvm'
                    connection = 'scm:git:https://github.com/alikemalocalan/greentunnel4jvm.git'
                    developerConnection = 'scm:git:ssh://git@github.com/alikemalocalan/greentunnel4jvm.git'
                }
            }
        }

    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/alikemalocalan/greentunnel4jvm")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
