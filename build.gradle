import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion

plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.1.10'
    id 'maven-publish'
    id 'application'
    id 'com.gradleup.shadow' version '8.3.6'
}

group = projectGroup
version = projectVersion

application {
    mainClass.set(projectMainClassName)
}

repositories {
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    api 'io.netty:netty-transport:4.1.118.Final'
    api 'com.squareup.okhttp3:okhttp-dnsoverhttps:5.0.0-alpha.14'
    implementation 'ch.qos.logback:logback-classic:1.5.17'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

kotlin {
    compilerOptions {
        // https://docs.gradle.org/current/userguide/compatibility.html#kotlin
        apiVersion = KotlinVersion.KOTLIN_2_1
        jvmTarget = JvmTarget.JVM_17
    }
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('')
    archiveVersion.set('')
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set("sources")
    from sourceSets.main.allSource, 'build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main'
}
publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/alikemalocalan/greentunnel4jvm")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
